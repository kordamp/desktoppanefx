/*
 * SPDX-License-Identifier: Apache-2.0
 *
 * Copyright 2015-2022 The original authors
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
plugins {
    id 'com.google.osdetector'
}

project.rootProject.ext.theVersion = file('VERSION').text.trim()

allprojects { p ->
    p.version = p.rootProject.ext.theVersion
}

ext.platform = osdetector.os == 'osx' ? 'mac' : osdetector.os == 'windows' ? 'win' : osdetector.os

config {
    info {
        name          = 'DesktopPaneFX'
        description   = 'MDI components for JavaFX'
        inceptionYear = '2015'
        tags          = ['mdi', 'javafx']

        specification { enabled = false }

        people {
            person {
                id = 'lincolnminto'
                name = 'Lincoln Minto'
                roles = ['developer']
            }
        }
    }

    docs {
        sourceXref {
            inputEncoding = 'UTF-8'
        }

        javadoc {
            autoLinks {
                enabled = false
            }
            options {
                addBooleanOption('javafx', true)
            }
            aggregate {
                enabled = false
            }
        }
    }
}

allprojects {
    apply plugin: 'idea'

    repositories {
        mavenLocal()
    }
}

idea {
    project {
        jdkName project.sourceCompatibility
        languageLevel project.sourceCompatibility

        ipr {
            withXml { provider ->
                def node = provider.asNode()
                node.component.find { it.'@name' == 'VcsDirectoryMappings' }?.mapping[0].'@vcs' = 'Git'
            }
        }
    }
}

subprojects {
    license {
        mapping {
            fxml = 'XML_STYLE'
        }
    }
}
